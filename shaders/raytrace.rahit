#version 460
#extension GL_NV_ray_tracing : require
#extension GL_GOOGLE_include_directive : enable
#extension GL_EXT_nonuniform_qualifier : enable
#extension GL_EXT_scalar_block_layout : enable

#include "binding.h"
#include "sampling.glsl"
#include "share.h"

////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Any hit
// - Will check the opacity value of the material at hit point
// - If it is transparent, it will randomly go through proportionaly to the opacity level
//

// Raytracing hit attributes: barycentrics
hitAttributeNV vec2 attribs;

// clang-format off

// Incoming Payload information
layout(location = 0) rayPayloadInNV RadianceHitInfo payload;

layout(set = 0, binding = 2) readonly buffer _InstanceInfo {PrimMeshInfo i[];} InstanceInfo;

layout(set = 1, binding = B_VERTICES) readonly buffer _VertexBuf {float v[];} VertexBuf;
layout(set = 1, binding = B_INDICES) readonly buffer _Indices {uint i[]; } indices;
layout(set = 1, binding = B_TEXCOORDS) readonly buffer _UvBuf {float v[];} UvBuf;
layout(set = 1, binding = B_MATERIAL) readonly buffer _MaterialBuffer {Material m[];} MaterialBuffer;
layout(set = 1, binding = B_TEXTURES) uniform sampler2D texturesMap[];
// clang-format on

// Shading structure
struct ShadingState
{
  vec2 texcoord0;
  uint matIndex;
  vec3 geom_normal;
};

// Return the vertex position
vec3 getVertex(uint index)
{
  vec3 vp;
  vp.x = VertexBuf.v[3 * index + 0];
  vp.y = VertexBuf.v[3 * index + 1];
  vp.z = VertexBuf.v[3 * index + 2];
  return vp;
}

// Retrieving the Uv coordinates
vec2 getTexCoord(uint index)
{
  vec2 vp;
  vp.x = UvBuf.v[2 * index + 0];
  vp.y = UvBuf.v[2 * index + 1];
  return vp;
}


//--------------------------------------------------------------
// Getting the interpolated vertex
// gl_InstanceID gives the Instance Info
// gl_PrimitiveID gives the triangle for this instance
//
ShadingState getShadingState()
{
  // Retrieve the Primitive mesh buffer information
  PrimMeshInfo pinfo = InstanceInfo.i[gl_InstanceCustomIndexNV];

  // Getting the 'first index' for this mesh (offset of the mesh + offset of the triangle)
  uint indexOffset  = pinfo.indexOffset + (3 * gl_PrimitiveID);
  uint vertexOffset = pinfo.vertexOffset;           // Vertex offset as defined in glTF
  uint matIndex     = max(0, pinfo.materialIndex);  // material of primitive mesh


  // Getting the 3 indices of the triangle (local)
  ivec3 triangleIndex = ivec3(indices.i[indexOffset + 0], indices.i[indexOffset + 1], indices.i[indexOffset + 2]);
  triangleIndex += ivec3(vertexOffset);  // (global)

  // Barycentric of the triangle
  const vec3 barycentric = vec3(1.0 - attribs.x - attribs.y, attribs.x, attribs.y);

  const vec3 pos0              = getVertex(triangleIndex.x);
  const vec3 pos1              = getVertex(triangleIndex.y);
  const vec3 pos2              = getVertex(triangleIndex.z);
  const vec3 geom_normal       = normalize(cross(pos1 - pos0, pos2 - pos0));
  const vec3 world_geom_normal = normalize(vec3(geom_normal * gl_WorldToObjectNV));

  // The Uv on each triangle point
  const vec2 uv0       = getTexCoord(triangleIndex.x);
  const vec2 uv1       = getTexCoord(triangleIndex.y);
  const vec2 uv2       = getTexCoord(triangleIndex.z);
  const vec2 texcoord0 = uv0 * barycentric.x + uv1 * barycentric.y + uv2 * barycentric.z;

  // Returning state information
  ShadingState state;
  state.texcoord0   = texcoord0;
  state.matIndex    = matIndex;
  state.geom_normal = world_geom_normal;
  return state;
}

bool is_back_face(ShadingState state)
{
  return dot(state.geom_normal, gl_WorldRayDirectionNV) > 0.0f;
}

void main()
{
  // Get the shading information
  ShadingState state = getShadingState();

  // Retrieve the material
  Material material = MaterialBuffer.m[state.matIndex];

  // Early return
  if(material.alphaMode == 0)  // opaque
    return;

  // Single sided, culling back face
  if(material.doubleSided == false && is_back_face(state))
    ignoreIntersectionNV();

  // Getting the opacity hold in the alpha channel
  float opacity = 1;
  if(material.shadingModel == 0)
  {
    opacity = material.pbrBaseColorFactor.a;
    if(material.pbrBaseColorTexture > -1)
      opacity *= texture(texturesMap[nonuniformEXT(material.pbrBaseColorTexture)], state.texcoord0).a;
  }
  else
  {
    opacity = material.khrDiffuseFactor.a;
    if(material.khrDiffuseTexture > -1)
      opacity *= texture(texturesMap[nonuniformEXT(material.khrDiffuseTexture)], state.texcoord0).a;
  }

  // Applying cutoff mask
  if(material.alphaMode == 1 && opacity < material.alphaCutoff)  // mask
    ignoreIntersectionNV();

  // Blending
  if(opacity == 0.0)  // fully transparent
    ignoreIntersectionNV();
  else if(rnd(payload.seed) > opacity)
    ignoreIntersectionNV();
}
